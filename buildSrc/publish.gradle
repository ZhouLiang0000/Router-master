apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

Properties config = new Properties()
config.load(project.file('../VERSION.properties').newDataInputStream())
def VERSION = config.getProperty("GRADLE_PLUGIN_VERSION")
def GROUP = 'com.chenenyu.router'
def ARTIFACT_ID = 'gradle-plugin'

group = GROUP
version = VERSION

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        bintray(MavenPublication) {
            from components.java
            groupId GROUP
            artifactId ARTIFACT_ID
            version VERSION

            artifact sourcesJar
        }
    }
}

bintray {
    Properties properties = new Properties()
    boolean hasLocalFile = false
    if (project.rootProject.file('local.properties').exists()) {
        hasLocalFile = true
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
    }
    user = hasLocalFile ? properties.getProperty("bintray.user") : ""
    key = hasLocalFile ? properties.getProperty("bintray.apikey") : ""

    publications = ['bintray']
    pkg {
        repo = "maven"
        name = "router-gradle-plugin"
        websiteUrl = 'https://github.com/chenenyu/Router'
        issueTrackerUrl = 'https://github.com/chenenyu/Router/issues'
        vcsUrl = 'https://github.com/chenenyu/Router.git'
        desc = "Router gradle plugin."
        labels = ["Android", "Router", "gradle", "gradle plugin"]
        licenses = ["Apache-2.0"]
        publicDownloadNumbers = true
        publish = true
    }
}
